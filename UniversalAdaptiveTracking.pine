//@version=6
indicator('Universal Adaptive Tracking', 'UAT', false, precision = 4, timeframe = '', timeframe_gaps = true)
//by gorx1


//  dependencies........................................................................................................
innovations_parameters(float datapoint, int length) =>
    var alpha           = 1 / length
    var ambiance        = 0.
    var control_curve   = 0.
    var peak            = 0.
    var bars_since_peak = 0

    if not na(datapoint)
        if datapoint > control_curve
            peak            := datapoint
            bars_since_peak := 0
        else
            bars_since_peak += 1

        ambiance      := na(ambiance[1]) ? datapoint : alpha * datapoint + (1 - alpha) * nz(ambiance[1])
        control_curve := peak * math.exp(-bars_since_peak * alpha)


    capacity     = math.log (length)
    order        = math.sqrt(length)
    crest_factor = (control_curve * capacity) / (ambiance * order) //  resolution-agnostic one 
    intensity    = crest_factor / math.pow(1 + math.pow(crest_factor / capacity, order), 1 / order) //  soft saturation


    [ambiance, control_curve, intensity]


mfpm(float data, int length) => //  Mechanical Feedback Price Model
    actual         = data
    forecast       = (data[1] + data[2]) / 2 // mech feedback based forecast for the next datapoint
    innovation     = actual - forecast
    abs_innovation = math.abs(innovation)
    neg_innovation = innovation < 0 ? abs_innovation : na
    pos_innovation = innovation > 0 ? abs_innovation : na

    [neg_ambiance, neg_control, neg_intensity] = innovations_parameters(neg_innovation, length)
    [pos_ambiance, pos_control, pos_intensity] = innovations_parameters(pos_innovation, length)


    [forecast, innovation, neg_ambiance, neg_control, neg_intensity, pos_ambiance, pos_control, pos_intensity]


universal_adaptive_tracking(float data, int length, float gamma, float innovation) =>
    sum   = 0.
    sum_w = 0.
    
    for i = 0 to length - 1
        x = math.pow((length - i) / (length + 1), gamma)
        //  "brought to you by beta(2, 2), all 5"
        gen =     math.pow(x, 3) - 0.5 * math.pow(x, 4) //  2 up
        cdf = 3 * math.pow(x, 2) - 2   * math.pow(x, 3) //  1 up
        pdf = 6 *          x     - 6   * math.pow(x, 2) //  main
        fly =              6 -     12  *          x     //  1 down
        box =                                   -12     //  2 down

        weight = gen * cdf * pdf * fly * box * math.abs(innovation[i])

        sum   += weight * data[i]
        sum_w += weight
        
    
    state = sum / sum_w


    state
//  dependencies........................................................................................................


//  main................................................................................................................
src = input(close, 'Source')
len = input(64   , 'Length')

[forecast, innovation, neg_ambiance, neg_control, neg_intensity,
                       pos_ambiance, pos_control, pos_intensity ] = mfpm(src, len)

weight = input(false, 'weight by innovation') ? innovation : 1

neg_state = universal_adaptive_tracking(src, len, neg_intensity, weight)
pos_state = universal_adaptive_tracking(src, len, pos_intensity, weight)

main_state = pos_intensity >= neg_intensity ? pos_state : neg_state
//  main................................................................................................................


//  visuals.............................................................................................................
viz_off = input(0, 'plotting offset', group = 'style')

//  lower study
color_inno = innovation > 0 ? pos_intensity > 1.5 ? color.purple : color.gray :
             innovation < 0 ? neg_intensity > 1.5 ? color.purple : color.gray : color.gray

plot(  innovation, 'innovation'       , color_inno, 1, plot.style_columns )
plot( pos_control, 'pos control curve', color.red , 1, plot.style_stepline)              
plot(-neg_control, 'neg control curve', color.blue, 1, plot.style_stepline)       

plot_pos_ambience = plot( pos_ambiance, 'pos ambiance', color.new(color.blue, 50), 1, plot.style_stepline)
plot_neg_ambience = plot(-neg_ambiance, 'neg ambiance', color.new(color.red , 50), 1, plot.style_stepline)
plot_line         = plot(0            , 'zero'                                                             )

fill(plot_line, plot_neg_ambience, color.new(color.red , 75))
fill(plot_line, plot_pos_ambience, color.new(color.blue, 75))

plot (neg_intensity, 'neg intensity', color.red , display = display.none)
plot (pos_intensity, 'pos intensity', color.blue, display = display.none)
hline(1.5                                       , display = display.none)

//  upper study
color_main_state = main_state > main_state[1] ? color.blue : main_state < main_state[1] ? color.red : color.purple


plot(main_state, 'main state', color_main_state, 2, offset = viz_off, force_overlay = true)

plot(neg_state - neg_control  * math.log (len), 'lower max vola band'    , color.gray      , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
plot(neg_state - neg_ambiance * math.sqrt(len), 'lower typical vola band', color.purple    , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
plot(neg_state                                , 'neg state'              , color.red       , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
plot(pos_state                                , 'pos state'              , color.blue      , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
plot(pos_state + pos_ambiance * math.sqrt(len), 'lower typical vola band', color.purple    , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
plot(pos_state + pos_control  * math.log (len), 'upper max vola band'    , color.gray      , 1, plot.style_stepline, offset = viz_off, force_overlay = true, display = display.none)
//  visuals.............................................................................................................


//  âˆž
