//@version=5
indicator('Quick scan for drift', 'QSFD', timeframe = '')

source = input       (close                                                          )
length = input       (256                                                            )
type_  = input.int   (0         , 'Type', minval = 0, maxval = 1                     )
data   = input.string('Original', options = ['Original', 'Differenced', 'Integrated'])

src = if data == 'Differenced'
    source - source[1]
else  if data == 'Integrated'
    ta.cum(source)
else
    source

len = length < 1 ? bar_index + 1 : length

qsfd(src, len, type_) =>
    slope = (src - nz(src[len - 1])) / len

    if type_ == 0
        dev_max = 0.0
        dev_min = 0.0

        for i = 0 to len - 1
            dev      = src[i] - (src - slope * i)
            dev_max := math.max(dev, dev_max)
            dev_min := math.min(dev, dev_min)

        slope / ((dev_max - dev_min)  / len)
    else
        slope / ((ta.range(src, len)) / len)

drift = nz(qsfd(src, len, type_), 1)

plot(drift, 'Drift', color.purple, 2, plot.style_cross)

hline(type_ == 0 ? -7.5 : -0.75, display = display.none)
hline(type_ == 0 ? -1.5 : -1                           )
hline(type_ == 0 ? -0.5 : -0.5                         )
hline(type_ == 0 ?  0.  :  0.                          )
hline(type_ == 0 ?  0.5 :  0.5                         )
hline(type_ == 0 ?  1.5 :  1                           )
hline(type_ == 0 ?  7.5 :  0.75, display = display.none)
